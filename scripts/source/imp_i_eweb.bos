#define TAK

#include "general/standard_commands.h"

piece  base, rod, leg1, leg2, leg3, rotate, gun, fpt;
piece  stormrot, pelvis, torso, head, luparm, lloarm, ruparm, rloarm, lthigh, lleg, rthigh, rleg;

static-var  bMoving, bAiming, runSpeed, maxAngle, randomSeed, packedUp, inBunker, maxSpeed;

#define SMALL_MUZZLE_FLASH_FX_RED 1024+0

CanCapture(capture) {
	capture = 1;
	if(bAiming) {
		capture = 0;
	}
	return 1;
}

Deploy() {
		runSpeed = 7;
		SET MAX_SPEED to 1;
		maxAngle = 1;
		packedUp = 0;
		turn pelvis to x-axis <20> speed <150>;
		turn head to x-axis <-10> speed <150>;
		turn torso to y-axis <0> speed <150>;
		move pelvis to y-axis [-1.25] speed [250];
		
		turn lthigh to x-axis <-60> speed <150>;
		turn lthigh to y-axis <20> speed <150>;
		turn lthigh to z-axis <0> speed <150>;
		turn lleg to x-axis <40> speed <150>;
		
		turn rthigh to x-axis <-50> speed <150>;
		turn rthigh to y-axis <-30> speed <150>;
		turn rthigh to z-axis <0> speed <150>;
		turn rleg to x-axis <40> speed <150>;
		
		turn luparm to x-axis <-21> speed <150>;
		turn luparm to y-axis <-13> speed <150>;
		turn luparm to z-axis <-32> speed <150>;
		
		turn lloarm to x-axis <-62> speed <150>;
		turn lloarm to y-axis <80> speed <150>;
		turn lloarm to z-axis <95> speed <150>;
		
		turn ruparm to x-axis <-21> speed <150>;
		turn ruparm to y-axis <13> speed <150>;
		turn ruparm to z-axis <32> speed <150>;
		
		turn rloarm to x-axis <-62> speed <150>;
		turn rloarm to y-axis <-80> speed <150>;
		turn rloarm to z-axis <-95> speed <150>;
		
		turn rod to x-axis <0> speed <40>;
		turn rod to y-axis <0> speed <80>;
		turn rod to z-axis <0> speed <80>;
		move pelvis to z-axis [0] speed [375];
		move rod to y-axis [0] speed [375];
		move rod to z-axis [0] speed [375];
		move rod to x-axis [0] speed [375];
		
		turn leg1 to x-axis <0> speed <150>;
		turn leg1 to y-axis <0> speed <150>;
		turn leg1 to z-axis <0> speed <150>;
		turn leg2 to x-axis <0> speed <150>;
		turn leg2 to y-axis <0> speed <150>;
		turn leg2 to z-axis <0> speed <150>;
		turn leg3 to x-axis <0> speed <150>;
		turn leg3 to y-axis <0> speed <150>;
		turn leg3 to z-axis <0> speed <150>;
		
		wait-for-turn ruparm around y-axis;
		wait-for-turn rloarm around x-axis;
		wait-for-turn rod around x-axis;
}

PackUp() {
		bAiming=0;
		runSpeed = 7;
		SET MAX_SPEED to 1;
		turn rotate to y-axis <0> speed <300>;
		turn stormrot to y-axis <0> speed <300>;
		wait-for-turn rotate around y-axis;

		move pelvis to y-axis [-2.5] speed [250];
		move pelvis to x-axis [1.25] speed [250];
		turn lthigh to x-axis <-80> speed <150>;
		turn lthigh to y-axis <20> speed <150>;
		turn lthigh to z-axis <0> speed <200>;
		turn lleg to x-axis <90> speed <150>;

		turn rthigh to x-axis <-80> speed <150>;
		turn rthigh to y-axis <-20> speed <150>;
		turn rthigh to z-axis <0> speed <200>;
		turn rleg to x-axis <80> speed <150>;

		turn luparm to x-axis <-21> speed <200>;
		turn luparm to y-axis <-13> speed <200>;
		turn luparm to z-axis <-32> speed <200>;
		turn lloarm to x-axis <26> speed <200>;
		turn lloarm to y-axis <80> speed <200>;
		turn lloarm to z-axis <90> speed <200>;

		turn ruparm to x-axis <90> speed <200>;
		turn ruparm to y-axis <0> speed <200>;
		turn ruparm to z-axis <0> speed <200>;
		turn rloarm to x-axis <-13> speed <200>;
		turn rloarm to z-axis <-34> speed <200>;
		sleep 300;


		turn rod to x-axis <-120> speed <200>;
		move pelvis to y-axis [1.25] speed [375];
		move pelvis to x-axis [0] speed [375];
		move rod to x-axis [-2.5] speed [375];
		move rod to y-axis [2.5] speed [375];

		turn pelvis to x-axis <10> speed <150>;
		turn head to x-axis <0> speed <150>;
		turn lthigh to x-axis <-30> speed <150>;
		turn lthigh to y-axis <0> speed <200>;
		turn lleg to x-axis <20> speed <150>;
		turn rthigh to x-axis <-30> speed <150>;
		turn rthigh to y-axis <0> speed <200>;
		turn rleg to x-axis <20> speed <150>;

		turn lloarm to x-axis <43> speed <150>;
		turn lloarm to y-axis <100> speed <150>;
		turn lloarm to z-axis <47> speed <150>;
		turn ruparm to x-axis <-80> speed <200>;
		turn ruparm to y-axis <10> speed <200>;
		turn rloarm to x-axis <-16> speed <150>;
		turn rloarm to y-axis <-20> speed <150>;
		turn rloarm to z-axis <-54> speed <150>;

		turn leg1 to x-axis <-60> speed <300>;
		turn leg2 to x-axis <16> speed <300>;
		turn leg2 to y-axis <100> speed <300>;
		turn leg2 to z-axis <54> speed <300>;
		turn leg3 to x-axis <16> speed <300>;
		turn leg3 to y-axis <-100> speed <300>;
		turn leg3 to z-axis <-54> speed <300>;
		sleep 300;


		turn pelvis to x-axis <0> speed <200>;
		move pelvis to y-axis [0] speed [250];
	
		turn lthigh to x-axis <0> speed <200>;
		turn lthigh to y-axis <0> speed <200>;
		turn lleg to x-axis <0> speed <200>;
		turn rthigh to x-axis <0> speed <200>;
		turn rthigh to x-axis <0> speed <200>;
		turn rleg to x-axis <0> speed <200>;

		move rod to y-axis [7.5] speed [375];
		move rod to z-axis [2.5] speed [375];
		move pelvis to z-axis [7.5] speed [375];

		turn lloarm to x-axis <-30> speed <200>;
		turn lloarm to y-axis <5> speed <200>;
		turn lloarm to z-axis <21> speed <200>;
	
		runSpeed = 7;
		SET MAX_SPEED to maxSpeed;
		maxAngle = 1;
		packedUp = 1;
		
	sleep 10;
	return(0);
}

setSFXoccupy(level) {
	if(level == 0) {
		var transport, tid;
		tid = get TRANSPORT_ID;
		transport = get COB_ID(tid);
		
		if(transport == 25) { //imp bunker
			inBunker = 1;
			bAiming = 1;
			start-script Deploy();
		}
		if(transport == 104) { // imp pillbox
			move fpt to y-axis [10] now;
			inBunker = 1;
			bAiming = 1;
			start-script Deploy();
		}
	}
	if(level != 0) {
		move fpt to y-axis [0] now;
		inBunker = 0;
		bAiming = 0;
		start-script PackUp();
	}
}

HitByWeaponId(z,x,id,damage) {
	if(inBunker) { return 0; }
	if(!inBunker) { return 100; }
}

Run() {
	if (bMoving == TRUE) {
		turn rleg to x-axis <85>/maxAngle speed <30>*runSpeed;	
		turn lleg to x-axis <25>/maxAngle speed <40>*runSpeed;
		turn rthigh to x-axis <-45>/maxAngle speed <20>*runSpeed;
		turn lthigh to x-axis <20>/maxAngle speed <20>*runSpeed;
	}
	if (bMoving == TRUE) { //run 2nd step
		turn rleg to x-axis <55>/maxAngle speed <60>*runSpeed;
		wait-for-turn lthigh around x-axis;

		turn lleg to x-axis <85>/maxAngle speed <30>*runSpeed;
		turn rleg to x-axis <25>/maxAngle speed <40>*runSpeed;
		turn lthigh to x-axis <-45>/maxAngle speed <20>*runSpeed;
	}
	if (bMoving == TRUE) { //run 2nd step
		turn rthigh to x-axis <20>/maxAngle speed <20>*runSpeed;

		turn lleg to x-axis <55>/maxAngle speed <60>*runSpeed;	
		wait-for-turn rthigh around x-axis;
	}
}

Activate() {
	if(bAiming) {
		return 0; }
	if(!packedUp and !bAiming) {
		call-script PackUp();
	}
}

Deactivate() {
	if(packedUp) {
		call-script Deploy();
	}
}

StartMoving() {
	bMoving = TRUE;
	return 0;
}

StopMoving() {
	bMoving = FALSE;
}

RestoreAfterDelay() {
	if(get ACTIVATION == TRUE) {
		sleep 3000;
		call-script PackUp();
	}
	bAiming = 0;
	return 0;
}

AimFromWeapon1(piecenum) {
	piecenum = head;
	return 0;
}

QueryWeapon1(piecenum) {
	piecenum = fpt;
	return 0;
}

FireWeapon1() {
	var burst, counter;
	burst = 2;
	counter = 0;
	while(counter < burst) {
		emit-sfx SMALL_MUZZLE_FLASH_FX_RED from fpt;
		move gun to z-axis [-0.5] speed [750];
		turn ruparm to x-axis <-15> speed <200>;
		turn luparm to x-axis <-15> speed <200>;
		turn torso to x-axis <-5> speed <200>;
		sleep 80;
		move gun to z-axis [0] speed [750];
		turn ruparm to x-axis <-21> speed <200>;
		turn luparm to x-axis <-21> speed <200>;
		turn torso to x-axis <0> speed <200>;
		sleep 80;
		++counter;
	}
	return 0;
}

AimWeapon1(heading,pitch) {
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1;
	if(inBunker != 1) {
		bAiming=1;
		call-script Deploy();
		turn rotate to y-axis heading speed <150>;
		turn stormrot to y-axis heading speed <150>;
		wait-for-turn rotate around y-axis;
		if(!inBunker) {
			start-script RestoreAfterDelay();
		}
		return 1;
	}
	return 0;
}

AimFromWeapon2(piecenum) {
	piecenum = head;
	return 0;
}

QueryWeapon2(piecenum) {
	piecenum = fpt;
	return 0;
}

FireWeapon2() {
	return 0;
}

AimWeapon2(heading,pitch) {
	if(inBunker == 1) {
		turn rotate to y-axis heading speed <150>;
		turn stormrot to y-axis heading speed <150>;
		wait-for-turn rotate around y-axis;
		return 1;
	}
	return 0;
}


MotionControl() {
	while(1) {
		if(bMoving) {
			if(!bAiming and packedUp) {
				call-script Run();
			}
		}
		if(!bMoving) {
			if(!bAiming) {
				if(packedUp) {
					turn rthigh to x-axis <0> speed <250>;
					turn rleg to x-axis <0> speed <250>;
					turn lthigh to x-axis <0> speed <250>;
					turn lleg to x-axis <0> speed <250>;
				}
			}
		}
		sleep randomSeed;
	}
}

Create() {
	hide fpt;
	randomSeed = rand(10,60);
	bMoving = FALSE;
	runSpeed = 7;
	maxSpeed = get MAX_SPEED;
	set MAX_SPEED to maxSpeed;
	maxAngle = 1;
	inBunker = 0;
	call-script PackUp();
	start-script MotionControl();
}

Killed(severity, corpsetype) {
}

#include "general/standard_commands.h"

piece  body, wings, larm, rarm, lgun, lpt, rgun, rpt, link, cockpit, r_flare, l_flare, rarm_FX, larm_FX;

static-var  gun_1;

#define SMOKEPIECE1 body
#define SMOKEPIECE2 rarm
#define SMOKEPIECE3 wings
#define SMOKEPIECE4 wings
#include "general/smokeunit_sws.h"

#define SMALL_MUZZLE_FLASH_FX_RED 1024+1

Create() {
	gun_1 = 0;
	turn rarm to z-axis <75> now;
	turn larm to z-axis <-75> now;
	start-script SmokeUnit_SWS();
}

QueryWeapon1(piecenum) {
	if(!gun_1) {
		piecenum = lpt;
	}
	if(gun_1) {
		piecenum = rpt;
	}
}

FireWeapon1() {
	if(!gun_1) {
		emit-sfx SMALL_MUZZLE_FLASH_FX_RED from l_flare;
	}
	if(gun_1) {
		emit-sfx SMALL_MUZZLE_FLASH_FX_RED from r_flare;
	}
	gun_1 = !gun_1;
}

AimWeapon1(heading, pitch) {
	turn lpt to x-axis 0 - pitch now;
	turn lpt to y-axis heading now;
	turn rpt to x-axis 0 - pitch now;
	turn rpt to y-axis heading now;
	return(1);
}

AimFromWeapon1(piecenum) {
	piecenum = cockpit;
}

QueryTransport(piecenum) {
	piecenum = link;
}

BeginTransport(height) {
	move link to y-axis (0 - height) now;
	turn rarm to z-axis <35> now;
	turn larm to z-axis <-35> now;
}

EndTransport() {
	turn rarm to z-axis <75> now;
	turn larm to z-axis <-75> now;
}

Killed(severity, corpsetype) {
	if(severity <= 25) {
		corpsetype = 1;
		explode body type BITMAP;
		return 0;
	}
	if(severity <= 50) {
		corpsetype = 2;
		explode body type BITMAP;
		return 0;
	}
	if(severity <= 99) {
		corpsetype = 3;
		explode body type BITMAP;
		return 0;
	}
	corpsetype = 3;
	explode body type BITMAP;
}

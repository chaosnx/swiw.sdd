#define TAK

#include "general/standard_commands.h"

piece  base, rocket, rf1, rf2, rotate, turret, b1, f1, b2, f2, wake1, wake2;

static-var  gun_1, gun_2, wake;

#define SIG_MOVE			16

#define SMOKEPIECE1 rocket
#define SMOKEPIECE2 rocket
#define SMOKEPIECE3 turret
#define SMOKEPIECE4 turret
#include "general/smokeunit_sws.h"

#define LARGE_MUZZLE_FLASH_FX_RED 1024+1

#define UNIT_ROOT base
#define UNIT_BELOW_GROUND_DIST [-40]
#define UNIT_RISE_SPEED [4]
#define DUSTFX 1024+2
#define DUSTFXPT1 base
#include "general/rebel_unit_build.h"

StartMoving() {
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	while(1) {
		if(wake <= 3) {
			emit-sfx 5 from wake1;
			emit-sfx 5 from wake2;
			emit-sfx 3 from wake1;
			emit-sfx 3 from wake2;
		}
		sleep 300;
	}
}

setSFXoccupy(crap) {
	wake = crap;
	if(wake > 2) {
		set UPRIGHT to 0;
	}
	if(wake < 3) {
		set UPRIGHT to 1;
	}
}

Create() {
	turn rocket to z-axis <50> now;
	hide f1;
	hide f2;
	hide rf1;
	hide rf2;
	gun_1 = 0;
	gun_2 = 0;
	start-script SmokeUnit_SWS();
	call-script ConstructionAnim();
	move base to y-axis 0 now;
}

RestoreAfterDelay() {
	sleep 2500;
	turn rotate to y-axis <0> speed <150>;
	turn turret to x-axis <0> speed <150>;
	turn rocket to y-axis <0> speed <150>;
	turn rocket to z-axis <50> speed <150>;
}

QueryWeapon1(piecenum) {
	if(!gun_1) {
		piecenum = f1; }
	if(gun_1) {
		piecenum = f2; }
}

AimFromWeapon1(piecenum) {
	piecenum = turret;
}

AimWeapon1(heading, pitch) {
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1;
	turn rotate to y-axis heading speed <150>;
	turn turret to x-axis 0 - pitch speed <150>;
	wait-for-turn rotate around y-axis;
	wait-for-turn turret around x-axis;
	start-script RestoreAfterDelay();
	return 1;
}

FireWeapon1() {
	if(!gun_1) {
		emit-sfx LARGE_MUZZLE_FLASH_FX_RED from f1;
		move b1 to z-axis [-2.5] speed [500];
		sleep 100;
		move b1 to z-axis [0] speed [125];
	}
	if(gun_1) {
		emit-sfx LARGE_MUZZLE_FLASH_FX_RED from f2;
		move b2 to z-axis [-2.5] speed [500];
		sleep 100;
		move b2 to z-axis [0] speed [125];
	}
	gun_1 = !gun_1;
}

QueryWeapon2(piecenum) {
	if(!gun_2) {
		piecenum = rf1; }
	if(gun_2) {
		piecenum = rf2; }
}

AimFromWeapon2(piecenum) {
	piecenum = rocket;
}

AimWeapon2(heading, pitch) {
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2;
	turn rocket to y-axis heading speed <150>;
	wait-for-turn rocket around y-axis;
	start-script RestoreAfterDelay();
	return 1;
}

FireWeapon2() {
	if(!gun_2) {
		emit-sfx LARGE_MUZZLE_FLASH_FX_RED from rf1; }
	if(gun_2) {
		emit-sfx LARGE_MUZZLE_FLASH_FX_RED from rf2; }
	gun_2 = !gun_2;
}

Killed(severity, corpsetype) {
	if(severity <= 25) {
		corpsetype = 1;
		explode base type BITMAP;
		return 0;
	}
	if(severity <= 50) {
		corpsetype = 2;
		explode turret type FALL | SMOKE | BITMAP;
		explode base type BITMAP;
		return 0;
	}
	if(severity <= 99) {
		corpsetype = 3;
		explode turret type FALL | SMOKE | BITMAP;
		explode base type BITMAP;
		return 0;
	}
	corpsetype = 3;
	explode turret type FALL | SMOKE | FIRE | BITMAP;
	explode base type BITMAP;
	return 0;
}
